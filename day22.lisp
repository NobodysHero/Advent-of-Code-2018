;;;; day22.lisp

(in-package :advent-of-code-2018)

(defun day22-parse-input ()
  (mapcar #'extract-integers (read-puzzlefile "input22.txt")))

(defun day22-generate-cave (width height cave-depth &optional (target-x 0) (target-y 0))
  "Generates the cave."
  (let ((indices (make-array (list (1+ width) (1+ height)) :element-type '(mod 20183))))
    ;; compute the geological indices
    (loop :for x :from 0 :upto width
          :do (setf (aref indices x 0) (mod (+ cave-depth (* x 16807)) 20183)))
    (loop :for y :from 0 :upto height
          :do (setf (aref indices 0 y) (mod (+ cave-depth (* y 48271)) 20183)))
    (loop :for y :from 1 :upto height
          :do (loop :for x :from 1 :upto width
                    :when (and (= x target-x) (= y target-y))
                    :do (setf (aref indices x y) 0)
                    :else :do (setf (aref indices x y)
                                    (mod
                                     (+ cave-depth
                                        (* (aref indices (1- x) y)
                                           (aref indices x (1- y))))
                                     20183))))
    ;; take mod 3 for type
    (loop :with cave := (make-array (list (1+ width) (1+ height)) :element-type '(mod 3))
          :for i :from 0 :below (array-total-size indices)
          :do (setf (row-major-aref cave i) (mod (row-major-aref indices i) 3))
          :finally (return cave))))

;;for debugging
(defun day22-print-cave (cave)
  "Used for debugging. Displayes the cave as seen on the AoC website."
  (loop :for y :below (array-dimension cave 1)
        :do (format t "狺ア祜镳骘衡屐秣ㄡ蝌狴溟礤铙轱汜鲥癌恒镬戾泗ㄣ狍盹ㄡ蝈汜鲥畅ò＼ū＼僵ú＼＼┅┅┅ㄤ彐躅溽膊箬矧翦篝疳翳ㄣ狯翎蜱弭翎蜱弭⑵轭潴翳箬矧翦篝疳翳骝镯翳艾泔矧溟钺翦麸翳翎蜱弭泔矧溟钺翦澡麸蜚轶羼蹰痧邃狒翳篝狎犷孱洚ㄦ戾è铄殓桠秕蝮ㄡ换铄殓桠秕蝮狎彘翳弪溟箴灬沐泔矧溟钺翦篝屦矧汨犷珏珏狎ㄤ弩趄蹉趱蜷铉忾钿珏狎祜镳烘矧ㄤ澌洪Жō癌ū癌ò暴ò暴烘矧铠航ǐ澍烘矧铢航ǐ澌瑚桢ㄡ钿ㄡ蝌狴轭怙躅潴汜鲥铠铢铒珏狎ㄡ蝈汜鲥铠铢┅┅恒镬戾泗扉篝铠铢珏狎洪铘铄殓桠秕蝮会痧孱珏狎汨犷珏麸翳铄殓桠秕蝮犷蝈趱蝾烘轭犰禊蝈趱蝾ㄣ镱扉篝ㄦ轵篝ㄤ屐弭珏狎ㄤ屐弭ㄡ蝈汜鲥扉篝博┅┅铄殓桠秕蝮┅┅ㄤ轶翎钽ㄡ猢戾è骝镯珏狎翳轵岍翎蜱弭珏狎翳轵猢┅ㄩ翎蜱弭珏狎骝镯珏狎珐┅箦泔钿眭祠轲戾鲠祯瀛扉篝ㄤ殛塍趄Ж暴扉篝翎蜱弭翎蜱弭暴＇溟篝犷沐＇铄殓桠秕蝮呼弩у聃犰皓┅┅ㄤ彐躅溽膊īㄤ弩趄蹉趱蜷铉忾钿è溴痿瑭翎蜱弭翎蜱弭┅ㄤ狴膊疳蝮瀛轭瘐舂戾è汜鲥ㄤ狴膊珏铄蜥翦汜鲥ǐ窗翎蜱弭ǐ窗翎蜱弭溴痿翎蜱弭翎蜱弭┅ㄦ矧磲⒃桢麸翎蜷箅轭溴轶狺ア祜镳烘矧乎痿翎蜱弭后蹴祜镳烘矧乎痿翎蜱弭后蹴ㄡ蝈汜鲥┅┅ㄦ矧磲⒃桢聃殂脲篝疳翳麸翳翎蜱弭翎脲黹铛翦螽アㄤ狴膊箬矧翦篝疳翳汜鲥翎蜱弭翎蜱弭┅┅